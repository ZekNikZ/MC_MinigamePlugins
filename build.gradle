apply plugin: 'idea'

ext {
  junitVersion = '5.7.0'
  spigotVersion = '1.19.2-R0.1-SNAPSHOT'
  protocolLibVersion = '5.0.0-SNAPSHOT'
  worldeditVersion = '7.2.12-SNAPSHOT'
  worldguardVersion = '7.0.8-SNAPSHOT'
  smartInvsVersion = '1.2.7'
  subserversClientVersion = '22w24a'
}

allprojects {
  repositories {
    mavenCentral()
    mavenLocal()
  }
}

subprojects {
  apply plugin: 'java'

  group = "io.zkz.mc.${rootProject.name}"

  repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url "https://maven.enginehub.org/repo/" }
  }

  dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    compileOnly "org.spigotmc:spigot-api:${spigotVersion}"
    compileOnly 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
    annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:16.0.2'

    if (project.hasProperty("resourceMainClass")) {
      implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    }
  }

  test {
    useJUnitPlatform()
  }

  task buildAll(type: Copy) {
    dependsOn build
    from('build/libs') {
      include "**/*.jar"
    }
    into '../build'
  }

  task resourceGen(type: JavaExec) {
    dependsOn build
    if (project.hasProperty("resourceMainClass")) {
      mainClass = project.property("resourceMainClass")
    }
    classpath = sourceSets.main.runtimeClasspath
  }
}

evaluationDependsOnChildren()

task deleteResourceCache(type: Delete) {
  delete 'build/resources'
}

task compileResourcePack(type: JavaExec) {
  dependsOn deleteResourceCache

  subprojects {
    if (project.hasProperty("resourceMainClass")) {
      dependsOn project.resourceGen
    }
  }

  mainClass = "io.zkz.mc.${rootProject.name}.gametools.resourcepack.ResourceManager"
  classpath = project('gametools').sourceSets.main.runtimeClasspath
}